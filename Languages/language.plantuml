@startebnf Mathematical RegExp Syntax

(* Current implementation process *)

Regex = Branch, Union;
Union = ["|", Branch, Union];

Branch = Expression, Concat;
Concat = [ "(", Regex, ")", DupSymbol, Concat
         | ? Symbol Token ?, DupSymbol, Concat
         | "[", BracketExpression_I, DupSymbol, Concat
         ];

Expression = "(", Regex, ")", DupSymbol
           | ? Symbol Token ?, DupSymbol
           | BracketExpression, DupSymbol
           ;

(* Duplication *)
DupSymbol = [ "*"
            | "?"
            | "+"
            | "{", Number, DupCount
            ];

DupCount = "}"
         | ",", MaxCount
         ;

MaxCount = "}"
         | Number, "}"
         ;

Number = ? Digit Token ?, Number_I;
Number_I = [? Digit Token ?, Number_I];

(* Simplified Bracket Expressions *)
BracketExpression = "[",  BracketExpression_I;
BracketExpression_I = "^", ? Symbol Token ?, Range, BracketList, "]"
                    | ? Symbol Token ?, Range, BracketList, "]" 
                    ;

BracketList = [ ? Symbol Token ?, Range, BracketList];

Range = ["-", ? Symbol Token ?];

@endebnf
